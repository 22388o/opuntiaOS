# Copyright 2021 Nikita Melekhin. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

uland_c_flags = [
  "-Werror",
  "-Wno-address-of-packed-member",
]

if (userland_symbols) {
  uland_c_flags += [ "-ggdb" ]
}

if (optimize) {
  uland_c_flags += [ "-Os" ]
}

uland_cc_flags = [
  "-std=c++2a",
  "-fno-sized-deallocation",
  "-fno-rtti",
  "-fno-exceptions",
  "-D_LIBCXX_BUILD_ONEOS_EXTENSIONS",
]

uland_asm_flags = []
uland_ld_flags = []

if (target_cpu == "x86") {
  uland_asm_flags += [
    "-f",
    "elf",
  ]
}

if (target_cpu == "aarch32") {
  uland_c_flags += [
    "-fno-builtin",
    "-march=armv7-a",
    "-mfpu=neon-vfpv4",
    "-mfloat-abi=softfp",
    "-fno-pie",
  ]
  uland_asm_flags += [
    "-march=armv7-a",
    "-mcpu=cortex-a15",
  ]
  uland_ld_flags += [ "-nostdlib" ]

  if (host == "gnu") {
    uland_ld_flags += [
      "-nostdinc",
      "-nodefaultlibs",
      "-nostartfiles",
      "-lgcc",
    ]
  }

  if (host == "llvm") {
    uland_ld_flags += [
      "--oformat",
      "elf32-littlearm",
      rebase_path("//toolchains/llvm_runtime/11.1.0/libclang_rt.builtins-arm.a", root_build_dir),
    ]
  }
}

config("userland_flags") {
  cflags = uland_c_flags
  cflags_cc = uland_cc_flags
  asmflags = uland_asm_flags
  ldflags = uland_ld_flags
  defines = [ "oneOS" ]
}

group("userland") {
  deps = [
    "//userland/about:about",
    "//userland/cat:cat",
    "//userland/dock:dock",
    "//userland/init:init",
    "//userland/initgui:initgui",
    "//userland/kill:kill",
    "//userland/ls:ls",
    "//userland/mkdir:mkdir",
    "//userland/onesh:onesh",
    "//userland/rm:rm",
    "//userland/rmdir:rmdir",
    "//userland/terminal:terminal",
    "//userland/touch:touch",
    "//userland/uname:uname",
    "//userland/utester:utester",
  ]
}
